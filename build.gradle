buildscript {
    ext.kotlin_version = '1.5.31'

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.android.tools.build:gradle:7.1.0-alpha12'
    }
}

allprojects {
    repositories {
        mavenCentral()
        google()
        maven { url "https://maven.google.com" }
        maven {
            url = "https://plugins.gradle.org/m2/"
        }
    }
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'

project.ext {
    groupId = 'com.github.leeonardoo'
    artifactId = 'actioncable-client-kotlin'
    libVersion = '0.0.17'
}

version = project.libVersion
group = project.groupId

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.5.31'
    implementation 'com.beust:klaxon:5.5'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'

    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit:1.5.31'
    testImplementation 'com.squareup.okhttp:mockwebserver:2.7.5'
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.java

                // You can then customize attributes of the publication as shown below.
                groupId = project.groupId
                artifactId = project.artifactId
                version = project.libVersion
            }
        }
    }
}